/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import AST.*;

parser code 
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");		
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print(":");
		System.out.print(lexer.getCharPos());
		System.out.print("] ");		
		System.exit(0);
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	//System.out.print(s.sym);
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getCharPos());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal TYPE_INT;
terminal TYPE_STRING;
terminal TYPE_VOID;
terminal ARRAY;
terminal ASSIGN;
terminal COMMA;
terminal EXTENDS;
terminal DOT;
terminal NIL;
terminal RETURN;
terminal IF;
terminal WHILE;
terminal NEW;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal DIVIDE;
terminal LT;
terminal GT;
terminal EQ;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal CLASS;

/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_LIST<AST_DEC> Program;
non terminal AST_EXP exp;
non terminal AST_VAR var;
non terminal AST_STMT stmt;
non terminal AST_LIST<AST_STMT> stmtList;
non terminal AST_DEC dec;
non terminal AST_TYPE type;
non terminal AST_ARRAY_TYPEDEF arrayTypedef;
non terminal AST_VAR_DEC varDec;
non terminal AST_FUNC_DEC funcDec;
non terminal AST_ARGUMENT argument;
non terminal AST_LIST<AST_ARGUMENT> argumentsList;
non terminal AST_CLASS_DEC classDec;
non terminal AST_LIST<AST_CFIELD> cFieldList;
non terminal AST_NEW_EXP newExp;
non terminal AST_CFIELD cField;
non terminal AST_LIST<AST_EXP> commaExpList;

/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence nonassoc ASSIGN;
precedence left EQ;
precedence left GT, LT;
precedence left PLUS, MINUS;
precedence left DIVIDE, TIMES;
precedence nonassoc LBRACK;
precedence nonassoc LPAREN;
precedence left DOT;

/************************/
/* START WITH: Program */
/************************/
start with Program;

/********************/
/* DERIVATION RULES */
/********************/
Program     ::= dec:d Program:dl                                                        {:RESULT = new AST_LIST<AST_DEC>(d, dl);:}
                | dec:d                                                                 {:RESULT = new AST_LIST<AST_DEC>(d, null); :}
                ;

dec         ::= varDec:vd                                                               {:RESULT = new AST_DEC(vd);:}
                | funcDec:fd                                                            {: RESULT = new AST_DEC(fd);:}
                | classDec:cd                                                           {: RESULT = new AST_DEC(cd);:}
                | arrayTypedef:atd                                                      {: RESULT = new AST_DEC(atd);:}
                ;

type        ::= TYPE_INT                                                                {:RESULT = new AST_TYPE("int");:}
                | TYPE_STRING                                                           {:RESULT = new AST_TYPE("string");:}
                | TYPE_VOID                                                             {:RESULT = new AST_TYPE("void");:}
                | ID:id                                                                 {:RESULT = new AST_TYPE("id", id);:}
                ;

arrayTypedef ::= ARRAY ID:id ASSIGN type:t LBRACK RBRACK SEMICOLON                      {:RESULT = new AST_ARRAY_TYPEDEF(id, t);:};

varDec       ::= type:t ID:id  SEMICOLON                                                {:RESULT = new AST_VAR_DEC<AST_EXP>(t, id, null);:}
                | type:t ID:id ASSIGN exp:e SEMICOLON                                   {:RESULT = new AST_VAR_DEC<AST_EXP>(t, id, e);:}
                | type:t ID:id ASSIGN newExp:ne SEMICOLON                               {:RESULT = new AST_VAR_DEC<AST_NEW_EXP>(t, id, ne);:}
                ;

funcDec     ::= type:t ID:id LPAREN RPAREN LBRACE stmtList:l RBRACE                     {:RESULT = new AST_FUNC_DEC(t, id, null, l);:}
                | type:t ID:id LPAREN argumentsList:al RPAREN LBRACE stmtList:sl RBRACE {:RESULT = new AST_FUNC_DEC(t, id, al, sl);:}
                ;

argument      ::= type:t ID:id                                                          {:RESULT = new AST_ARGUMENT(t, id);:};

argumentsList ::= argument:a                                                            {:RESULT = new AST_LIST<AST_ARGUMENT>(a, null);:}
                  | argument:a COMMA argumentsList:al                                   {:RESULT = new AST_LIST<AST_ARGUMENT>(a, al);:}
                  ;

classDec    ::= CLASS ID:id LBRACE cFieldList:l RBRACE                                  {:RESULT = new AST_CLASS_DEC(id, l);:}
                | CLASS ID:cId EXTENDS ID:eId LBRACE cFieldList:l RBRACE                {:RESULT = new AST_CLASS_DEC(cId, eId, l);:}
                ;

cFieldList  ::= cField:c                                                                {:RESULT = new AST_LIST<AST_CFIELD>(c, null);:}
                | cField:c cFieldList:cl                                                {:RESULT = new AST_LIST<AST_CFIELD>(c, cl);:}
                ;

stmt        ::=     varDec:v                                                            {: RESULT = new AST_STMT_VAR_DEC(v); :}
                    | var:v ASSIGN exp:e SEMICOLON                                      {: RESULT = new AST_STMT_ASSIGN<AST_EXP>(v, e); :}
                    | var:v ASSIGN newExp:e SEMICOLON                                   {: RESULT = new AST_STMT_ASSIGN<AST_NEW_EXP>(v, e); :}
                    | RETURN SEMICOLON                                                  {: RESULT = new AST_STMT_RET(null); :}
                    | RETURN exp:e SEMICOLON                                            {: RESULT = new AST_STMT_RET(e); :}
                    | IF LPAREN exp:e RPAREN LBRACE stmtList:l RBRACE                   {: RESULT = new AST_STMT_IF(e, l); :}
                    | WHILE LPAREN exp:e RPAREN LBRACE stmtList:l RBRACE                {: RESULT = new AST_STMT_WHILE(e, l); :}
                    | ID:id LPAREN RPAREN SEMICOLON                                     {: RESULT = new AST_STMT_ID(null, id, null); :}
                    | ID:id LPAREN commaExpList:l RPAREN SEMICOLON                      {: RESULT = new AST_STMT_ID(null, id, l); :}
                    | var:v DOT ID:id LPAREN RPAREN SEMICOLON                           {: RESULT = new AST_STMT_ID(v, id, null); :}
                    | var:v DOT ID:id LPAREN commaExpList:l RPAREN SEMICOLON            {: RESULT = new AST_STMT_ID(v, id, l); :}
                    ;

stmtList	::=		stmt:s	stmtList:l		                                            {: RESULT = new AST_LIST<AST_STMT>(s, l);    :}
                    | stmt:s			                                                {: RESULT = new AST_LIST<AST_STMT>(s, null); :}
					;

commaExpList ::=     exp:e COMMA commaExpList:l                                         {: RESULT = new AST_LIST<AST_EXP>(e, l); :}
                    | exp:e                                                             {: RESULT = new AST_LIST<AST_EXP>(e, null); :}
                    ;

newExp      ::=     NEW type:t                                                          {: RESULT = new AST_NEW_EXP_NEW_TYPE_EXP(t, null); :}
                    | NEW type:t LBRACK exp:e RBRACK                                    {: RESULT = new AST_NEW_EXP_NEW_TYPE_EXP(t, e); :}
                    ;

cField      ::=     varDec:v                                                            {: RESULT = new AST_CFIELD_DEC<AST_VAR_DEC>(v); :}
                    | funcDec:f                                                         {: RESULT = new AST_CFIELD_DEC<AST_FUNC_DEC>(f); :}
                    ;

exp         ::= var:v                                                                   {:RESULT = new AST_EXP_VAR(v); :}
                | LPAREN exp:e RPAREN                                                   {:RESULT = new AST_EXP_EXP(e); :}

                | exp:e1 PLUS exp:e2                                                    {:RESULT = new AST_EXP_BINOP(e1, e2, 0); :}
                | exp:e1 MINUS exp:e2                                                   {:RESULT = new AST_EXP_BINOP(e1, e2, 1); :}
                | exp:e1 TIMES exp:e2                                                   {:RESULT = new AST_EXP_BINOP(e1, e2, 2); :}
                | exp:e1 DIVIDE exp:e2                                                  {:RESULT = new AST_EXP_BINOP(e1, e2, 3); :}
                | exp:e1 LT exp:e2                                                      {:RESULT = new AST_EXP_BINOP(e1, e2, 4); :}
                | exp:e1 GT exp:e2                                                      {:RESULT = new AST_EXP_BINOP(e1, e2, 5); :}
                | exp:e1 EQ exp:e2                                                      {:RESULT = new AST_EXP_BINOP(e1, e2, 6); :}

                | ID:id LPAREN RPAREN                                                   {:RESULT = new AST_EXP_ID(id, null, null); :}
                | var:v DOT ID:id LPAREN RPAREN                                         {:RESULT = new AST_EXP_ID(id, v, null); :}
                | ID:id LPAREN commaExpList:l RPAREN                                    {:RESULT = new AST_EXP_ID(id, null, l); :}
                | var:v DOT ID:id LPAREN commaExpList:l RPAREN                          {:RESULT = new AST_EXP_ID(id, v, l); :}

                | MINUS INT:i                                                           {:RESULT = new AST_EXP_OPT(i, null, 0); :}
                | INT:i                                                                 {:RESULT = new AST_EXP_OPT(i, null, 0); :}
                | STRING:s                                                              {:RESULT = new AST_EXP_OPT(null, s, 1); :}
                | NIL
                ;

var          ::= ID:id                                                                  {: RESULT = new AST_VAR_ID(id); :}
                 | var:v DOT ID:id                                                      {: RESULT = new AST_VAR_VAR_ID(v, id); :}
                 | var:v LBRACK exp:e RBRACK                                            {: RESULT = new AST_VAR_EXP(v, e); :}
                 ;